call_gate:
        ;-----------------------------------
        ;スタックフレームの構築
        ;-----------------------------------
                                            ;   +12| X(列)
                                            ;   +16| Y(行)
                                            ;   +20| 色
                                            ;   +24| 文字
                                            ;   + 8| CS(コードセグメント)
                                            ;   + 4| EIP(戻り番号)
        push   ebp                          ;EBP+ 0| EBP(元の値)
        mov    ebp, esp                     ;------|---------

        ;-----------------------------------
        ;レジスタの保存
        ;-----------------------------------
        pusha
        push    ds
        push    es

        ;---------------------------------------
        ;データ用セグメントの設定
        ;---------------------------------------
        mov     ax, 0x0010
        mov     ds, ax
        mov     es, ax
        
        ;---------------------------------------
        ;文字を表示
        ;---------------------------------------
        mov     eax, dword [ebp +12]            ;EAX = X(列);
        mov     ebx, dword [ebp +16]            ;EBX = Y(行);
        mov     ecx, dword [ebp +20]            ;ECX = 色;
        mov     edx, dword [ebp +24]            ;EDX = 文字;
        cdecl   draw_str, eax, ebx, ecx, edx    ;draw_str();

        ;---------------------------------------
        ;レジスタの復帰
        ;---------------------------------------
        pop     es
        pop     ds
        popa

        ;---------------------------------------
        ;スタックフレームの破棄
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        retf 4 * 4