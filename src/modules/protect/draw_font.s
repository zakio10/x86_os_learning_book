draw_font:
        ;-----------------------------------
        ;スタックフレームの構築
        ;-----------------------------------
                                            ;   +12| Y(行)
                                            ;   + 8| X(列)
                                            ;   + 4| IP(戻り番号)
        push   ebp                          ;EBP+ 0| EBP(元の値)
        mov    ebp, esp                     ;------|---------

        ;-----------------------------------
        ;レジスタの保存
        ;-----------------------------------
        push    eax
        push    ebx
        push    ecx
        push    edi
        push    esi

        ;---------------------------------------
        ;コピー元フォントアドレスを設定
        ;---------------------------------------
        mov     esi, [ebp + 8]                  ;ESI = X(列)
        mov     edi, [ebp +12]                  ;EDI = Y(行)

        ;---------------------------------------
        ;コピー先アドレスを取得
        ;---------------------------------------
        mov     ecx, 0                          ;for(ECX = 0;
.10L:   cmp     ecx, 256                        ;    ECX < 256;
        jae     .10E                            ;
                                                ;    ECX++)
                                                ;{
        ;---------------------------------------
        ;表示する桁位置を計算
        ;---------------------------------------
        mov     eax, ecx                        ;   EAX = ECX;
        and     eax, 0x0F                       ;   EAX &= 0x0F;
        add     eax, esi                        ;   EAX += X;
        ;---------------------------------------
        ;表示する行位置を計算
        ;---------------------------------------
        mov     ebx, ecx                        ;   EBX = ECX;
        shr     ebx, 4                          ;   EBX /= 16;
        add     ebx, edi                        ;   EBX += Y;
        
        cdecl   draw_char, eax, ebx, 0x07, ecx  ;   draw_char();

        inc     ecx                             ;   //for(... ECX++)
        jmp     .10L                            ;
.10E:                                           ;}

        ;---------------------------------------
        ;レジスタの復帰
        ;---------------------------------------
        pop     esi
        pop     edi
        pop     ecx
        pop     ebx
        pop     eax

        ;---------------------------------------
        ;スタックフレームの破棄
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret