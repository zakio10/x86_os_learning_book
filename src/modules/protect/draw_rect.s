draw_rect:
        ;-----------------------------------
        ;スタックフレームの構築
        ;-----------------------------------
                                            ;   +24| 色
                                            ;   +20| Y1(終点のY座標)
                                            ;   +16| X1(終点のX座標)
                                            ;   +12| Y0(始点のY座標)
                                            ;   + 8| X0(始点のX座標)
                                            ;   + 4| IP(戻り番号)
        push    ebp                         ;EBP+ 0| EBP(元の値)
        mov     ebp, esp                    ;------|---------
        ;-----------------------------------
        ;レジスタの保存
        ;-----------------------------------
        push    eax
        push    ebx
        push    ecx
        push    edx
        push    esi

        ;---------------------------------------
        ;変数に引数を代入
        ;---------------------------------------
        mov     eax, [ebp + 8]                  ;EAX = X0;
        mov     ebx, [ebp +12]                  ;EBX = Y0;
        mov     ecx, [ebp +16]                  ;ECX = X1;
        mov     edx, [ebp +20]                  ;EDX = Y1;
        mov     esi, [ebp +24]                  ;ESI = 色;

        ;---------------------------------------
        ;座標軸の大小を確定
        ;---------------------------------------
        cmp     eax, ecx                        ;if(X1 < X0)
        jl      .10E                            ;{
        xchg    eax, ecx                        ;   //X0とX1を入れ替える;
.10E:                                           ;}

        cmp     ebx, edx                        ;if(Y1 < Y0)
        jl      .20E                            ;{
        xchg    ebx, edx                        ;   //Y0とY1を入れ替える;
.20E:                                           ;}

        ;---------------------------------------
        ;矩形を描画
        ;---------------------------------------
        cdecl   draw_line, eax, ebx, ecx, ebx, esi  ;上線
        cdecl   draw_line, eax, ebx, eax, edx, esi  ;左線

        dec     edx                                 ;EDX--; //下線は1ドット上げる
        cdecl   draw_line, eax, edx, ecx, edx, esi  ;下線
        inc     edx

        dec     ecx                                 ;ECX--; //右線は1ドット左に移動
        cdecl   draw_line, ecx, ebx, ecx, edx, esi  ;右線

        ;---------------------------------------
        ;レジスタの復帰
        ;---------------------------------------
        pop     esi
        pop     edx
        pop     ecx
        pop     ebx
        pop     eax

        ;---------------------------------------
        ;スタックフレームの破棄
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret