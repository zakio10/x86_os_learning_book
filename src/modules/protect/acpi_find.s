acpi_find:
        ;----------------------------------------
        ;スタックフレームの構築
        ;----------------------------------------
                                                ;   + 8| 入力されたキーコード
                                                ;   + 4| EIP(戻り番号)
        push   ebp                              ;EBP+ 0| EBP(元の値)
        mov    ebp, esp                         ;------|---------

        ;----------------------------------------
        ;レジスタの保存
        ;----------------------------------------
        push    ecx
        push    edi

        ;---------------------------------------
        ;引数を取得
        ;---------------------------------------
        mov     edi, [ebp + 8]                  ;EDI = アドレス;
        mov     ecx, [ebp +12]                  ;ECX = サイズ;
        mov     eax, [ebp +16]                  ;EAX = 検索データ;

        ;---------------------------------------
        ;名前の検索
        ;---------------------------------------
        cld                                     ;//DFくりあ　(+方向)
.10L:                                           ;while(1)
                                                ;{
        repne   scasb                           ;   while(AL != *EDI) EDI++;
        
        cmp     ecx, 0                          ;   if(0 == ECX)
        jnz     .11E                            ;   {
        mov     eax, 0                          ;       EAX = 0; //見つからない
        jmp     .10E                            ;       break;
.11E:                                           ;   }

        cmp     eax, [es:edi - 1]               ;   if(EAX != *EDI) //4文字分一致?
        jne     .10L                            ;       continue; //不一致
        dec     edi                             ;   EAX = EDI - 1;
        mov     eax, edi
.10E:                                           ;}

        ;---------------------------------------
        ;レジスタの復帰
        ;---------------------------------------
        pop     edi
        pop     ecx

        ;---------------------------------------
        ;スタックフレームの破棄
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret